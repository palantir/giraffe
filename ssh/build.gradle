apply from: '../gradle/shared.gradle'

apply plugin: 'giraffe.system-test-files'

dependencies {
    compile project(':giraffe-core')

    compile group: 'org.slf4j', name: 'slf4j-api', version: libVersions.slf4j
    compile(group: 'com.hierynomus', name: 'sshj', version: libVersions.sshj) {
        // force our version of slf4j-api in a way that persists
        exclude module: 'slf4j-api'
    }

    testCompile project(path: ':giraffe-fs-base', configuration: 'systemTest')
    testCompile project(path: ':giraffe-core', configuration: 'systemTest')

    testCompile(group: 'org.apache.sshd', name: 'sshd-core', version: libVersions.sshd) {
        // exclude optional Maven dependencies
        exclude module: 'tomcat-apr'
    }
    testCompile(group: 'org.apache.sshd', name: 'sshd-sftp', version: libVersions.sshd)
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: libVersions.logback
}

systemTestFiles {
    creators {
        file {
            source project(':giraffe-fs-base')
        }

        exec {
            source project(':giraffe-core')
        }
    }
}

// configure unit tests
test {
    include '**/EmbeddedSsh*Suite*'
}

task linuxIntegrationTest(type: Test) {
    include '**/Linux*Suite*'

    systemProperty 'giraffe.test.baseDir', '/home/giraffe'
    systemProperty 'giraffe.test.host', 'giraffe@localhost'
    systemProperty 'giraffe.test.hostKey', '/home/circleci/.ssh/integration-test'
}

jacocoCoverage {
    packageThreshold 0.45, 'com/palantir/giraffe'
}
