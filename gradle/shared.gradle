apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'jacoco-coverage'

apply plugin: 'com.github.hierynomus.license'

apply from: rootProject.file('gradle/versions.gradle')

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = rootProject.version
group = rootProject.group

repositories {
    mavenCentral()
}

checkstyle {
    configFile rootProject.file('config/checkstyle/checkstyle.xml')
}

eclipse {
    classpath {
        defaultOutputDir = file('ecbuild')

        def containerType = 'org.eclipse.jdt.launching.JRE_CONTAINER'
        containers.remove(containerType)
        containers "${containerType}/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7"
    }
}

eclipseJdt {
    inputFile = rootProject.file('config/eclipse/org.eclipse.jdt.core.prefs')
    doLast {
        copy {
            from rootProject.file('/config/eclipse/org.eclipse.jdt.ui.prefs')
            into "${projectDir}/.settings"
        }
    }
}

tasks.withType(Test).all { test ->
    test.testLogging {
        showExceptions true
        exceptionFormat 'full'

        info {
            showStackTraces true
        }
    }
}

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:6.6'

    testCompile group: 'junit', name: 'junit', version: libVersions.junit
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: libVersions.hamcrest
}

apply from: rootProject.file('gradle/javadoc.gradle'), to: javadoc

license {
    header rootProject.file('config/HEADER')
    skipExistingHeaders true

    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    include '**/*.java'
}
