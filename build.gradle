buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.palantir:gradle-flexversion:0.7.0'
        classpath 'com.palantir:jacoco-coverage:0.1.4'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'gradle-flexversion'
apply from: 'gradle/versions.gradle'

addPrintVersionTask()
flexversion {
    envvarSources << 'TRAVIS_BRANCH'
    useTags = true
}

version flexVersion()
group = 'com.palantir.giraffe'

ext.localPublishDir = "${buildDir}/localpublish"

task distZip(type: Zip) {
    dependsOn (['core', 'ssh', 'fs-base', 'test-util'].collect { ":giraffe-${it}:publish" })

    from (localPublishDir) {
        exclude '**/maven-metadata.xml*'
    }

    baseName = project.name
    appendix = 'release'
    version = project.version

    destinationDir = file("${buildDir}/distributions")
    doFirst {
        file(destinationDir).mkdirs()
    }
}

task publish(dependsOn: distZip)

task clean(type: Delete) {
    delete buildDir
}
